buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile "org.jsoup:jsoup:1.10.3"
}

task sourceJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allJava
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact tasks.sourceJar
            groupId 'me.ghui'
            artifactId 'Fruit'
            version "$fruitVersion"
            pom.withXml {
                asNode().dependencies.'*'.findAll() {
                    it.scope.text() == 'runtime'  && project.configurations.compile.allDependencies.find { dep ->
                        dep.name == it.artifactId.text()
                    }
                }.each() {
                    it.scope*.value = 'compile'
                }
            }
        }
    }

}

bintray {
    user = project.hasProperty('bintrayUser') ? project.bintrayUser : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayKey') ? project.bintrayKey : System.getenv('BINTRAY_KEY')
    publications = ['MyPublication']
    pkg {
        repo = 'Java'
        name = 'Fruit'
        licenses = ['Apache-2.0']
        vcsUrl = 'git@github.com:ghuiii/Fruit.git'
        version {
            name = "$fruitVersion"
            released = new Date()
            vcsTag = "$fruitVersion"
        }
    }
}
