apply plugin: 'java'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

sourceCompatibility = "1.7"
targetCompatibility = "1.7"

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile "me.ghui:Fruit:1.0.4"
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.okhttp3:mockwebserver:3.9.0'
    compile 'org.assertj:assertj-core:3.8.0'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

ext {
    libVersion = '1.0.0'
    libName = 'fruit-converter-retrofit'
    libUrl = 'https://github.com/ghuiii/Fruit/tree/master/converter-retrofit'
    desc = "A fruit converter for retrofit"
    license = "Apache-2.0"
}

// Create the pom configuration:
def pomConfig = {
    licenses {
        license {
            name "$license"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
        }
    }
    developers {
        developer {
            id "ghui"
            name "ghui"
            email "ghuiii.zhang@gmail.com"
        }
    }

    scm {
        url "$libUrl"
    }
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'me.ghui'
            artifactId "$libName"
            version "$libVersion"
            pom.withXml {
                def root = asNode()
                root.appendNode('description', "$desc")
                root.appendNode('name', "$libName")
                root.appendNode('url', "$libUrl")
                root.children().last() + pomConfig
            }
        }
    }

}

bintray {
    user = project.hasProperty('bintray_user') ? bintray_user : ''
    key = project.hasProperty('bintray_key') ? bintray_key : ''
    println("user: " + user + ", key: " + key)
    publications = ['MyPublication']
    pkg {
        repo = 'Java'
        name = "$libName"
        licenses = ["$license"]
        vcsUrl = "$libUrl"
        version {
            name = "$libVersion"
            desc = "$desc"
            released = new Date()
            vcsTag = "$libVersion"

            gpg {
                sign = true //Determines whether to GPG sign the files. The default is false
            }
            mavenCentralSync {
                sync = true //[Default: true] Determines whether to sync the version to Maven Central.
                user = project.hasProperty('oss_user') ? project.oss_user : ''
                password = project.hasProperty('oss_psw') ? project.oss_psw : ''
                println("oss_user: " + user + ", oss_psw: " + password)
                close = '1' //Optional property. By default the staging repository is closed and artifacts are released to Maven Central. You can optionally turn this behaviour off (by puting 0 as value) and release the version manually.
            }
        }
    }
}
