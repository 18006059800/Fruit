buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile "org.jsoup:jsoup:1.10.3"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

ext {
    fruitVersion = '1.0.4'
    fruitName = 'Fruit'
    fruitUrl = 'https://git@github.com:ghuiii/Fruit.git'
    desc = "A java library which can be used to convert html to Java Objects"
    license = "Apache-2.0"
}

// Create the pom configuration:
def pomConfig = {
    licenses {
        license {
            name "$license"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
        }
    }
    developers {
        developer {
            id "ghui"
            name "ghui"
            email "ghuiii.zhang@gmail.com"
        }
    }

    scm {
        url "$fruitUrl"
    }
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'me.ghui'
            artifactId "$fruitName"
            version "$fruitVersion"
            pom.withXml {
                def root = asNode()
                root.appendNode('description', "$desc")
                root.appendNode('name', "$fruitName")
                root.appendNode('url', "$fruitUrl")
                root.children().last() + pomConfig
            }
        }
    }

}

bintray {
    user = project.hasProperty('bintray_user') ? bintray_user : ''
    key = project.hasProperty('bintray_key') ? bintray_key : ''
    println("user: " + user + ", key: " + key)
    publications = ['MyPublication']
    pkg {
        repo = 'Java'
        name = "$fruitName"
        licenses = ["$license"]
        vcsUrl = "$fruitUrl"
        version {
            name = "$fruitVersion"
            desc = "$desc"
            released = new Date()
            vcsTag = "$fruitVersion"

            gpg {
                sign = true //Determines whether to GPG sign the files. The default is false
            }
            mavenCentralSync {
                sync = true //[Default: true] Determines whether to sync the version to Maven Central.
                user = project.hasProperty('oss_user') ? project.oss_user : ''
                password = project.hasProperty('oss_psw') ? project.oss_psw : ''
                println("oss_user: " + user + ", oss_psw: " + password)
                close = '1' //Optional property. By default the staging repository is closed and artifacts are released to Maven Central. You can optionally turn this behaviour off (by puting 0 as value) and release the version manually.
            }
        }
    }
}
